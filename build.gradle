import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.deokhugam'
version = '0.0.1-SNAPSHOT'
description = 'Deokhugam'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

processResources {
    filesMatching('**/application.yml') {
        filter(ReplaceTokens, tokens: [version: project.version])
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.postgresql:postgresql'

    implementation 'io.micrometer:micrometer-registry-cloudwatch2:1.17.0-M1'
    implementation platform('software.amazon.awssdk:bom:2.29.0')
    implementation 'software.amazon.awssdk:s3'

    //healthcheck
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.owasp.encoder:encoder:1.2.3'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'io.findify:s3mock_2.13:0.2.6'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.0'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:5.0.0:jakarta"
//    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"

}



tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
// ./gradlew externalTest 명령어를 통해 실행 가능합니다

jacoco {
    toolVersion = "0.8.8" // 버전 명시
}

def jacocoExcludes = [
        // 스프링 배치 제외
        '**/batch/**',
        '**/scheduler/**',
        '**/policy/**',

        // 커스텀 예외 제외
        '**/exception/**/*.class',

        // dto 제외
        '**/dto/**',

        // config 제외
        '**/config/**',
        '**/*Config.class'
]

jacocoTestReport {
    dependsOn test // 리포트 생성 전에 test를 반드시 수행해야 한다!
    reports { // 어떤 파일들을 생성할지, 어디에 생성할지 설정
        xml.required.set(true) // Gradle 8+: enabled → required
        html.required.set(true)
        // 경로 명시 안 할 경우 기본 경로는 build/reports/jacoco 이하 경로
    }

    classDirectories.setFrom(files(classDirectories.files.collect { dir ->
        fileTree(dir: dir, exclude: jacocoExcludes)
    }))

}
